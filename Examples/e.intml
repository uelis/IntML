letw return = fun x -> inl(x)
letw continue = fun x -> inr(x)

letw fixw = fun x ->
  case x of 
    inl(lx) -> 
      let (l,x) = lx in
        case x of
          inl(bq) ->
            let (b,q) = bq in
              inl(fold<'c.1+'b*'c> (inr(b,l)), inr(q))
        | inr(a) ->
            let r = unfold<'c.1+'b*'c> l in
(*            let u = delete<'c.1+'b*'c> l in *)
            case r of
              inl(nl) -> 
                 inr(a)
            | inr(cns) ->
               let (hd, tl) = cns in
                 inl(tl, inl(hd, a))
  | inr(q) -> 
     inl(fold<'c.1+'b*'c> inl(), inr(q))

letu fix = hack fixw
 as {mu 'c. 1 + 'b*'c}({'b}'a --o 'a) --o 'a

letu nil : [int] =
    [0]
(*
type lazylist<'a> = 
        Susp of cont<cont<'a>>
      | Cons of (cont<cont<'a>> + int) * lazylist<'a>
*)
letw Susp = fun x -> 
      fold<'a. cont<cont<'a>> + (cont<cont<int>> + int) * 'a> inl(x)
letw Cons = fun x -> 
      fold<'a. cont<cont<'a>> + (cont<cont<int>> + int) * 'a> inr(x)

(* Invariante: Wenn man inl(k) einmal forced, dann kommt nicht wieder
 * eine Liste der Form inl(...) heraus *)
letu cons : [int] --> [mu 'a. cont<cont<'a>> + (cont<cont<int>> + int) * 'a]
                  --> [mu 'a. cont<cont<'a>> + (cont<cont<int>> + int) * 'a] =  
  (fun x -> fun xs ->
    let [xk] = suspend(x) in
    let [xsk] = suspend(xs) in
    let [id] = [ Cons(inl(xk), Susp(xsk)) ] in
        [id])

letu ft = 
  fun l -> 
    let [lid] = l in
    case unfold<'a. cont<cont<'a>> + (cont<cont<int>> + int) * 'a> lid of
      inl(thunk) -> 
        let [v] = force thunk in
        let [u] = [ lid :=<'a. cont<cont<'a>> + (cont<cont<int>> + int) * 'a> 
                         unfold<'a.cont<cont<'a>> + (cont<cont<int>> + int) * 'a> v ] in
          [lid]
    | inr(v) -> [lid]

letu head =
  fun l -> 
    let [lid] = ft l in
    case unfold<'a. cont<cont<'a>> + (cont<cont<int>> + int) * 'a> lid of
      inl(thunk) -> 
        [let x=0 loop inr(x)]
    | inr(ht) ->
        let [h] = [let (h,t) = ht in h] in
        let [t] = [let (h,t) = ht in t] in
        case h of
          inl(k) -> 
            let [v] = force(k) in
            let [u] = [ lid :=<'a. cont<cont<'a>> + (cont<cont<int>> + int) * 'a> inr(inr(v), t) ] in
              [v]
        | inr(v) -> 
              [v]

letu tail =
  fun l -> 
    let [lid] = ft l in
    case unfold<'a. cont<cont<'a>> + (cont<cont<int>> + int) * 'a> lid of
      inl(thunk) -> 
        [let x=0 loop inr(x)]
    | inr(ht) ->
        [let (h,t) = ht in t]

letu thunk =
  fun l -> 
    (* Invariante erhalten *)
    let [thunk] =  suspend (
                      let [lid] = ft l in [lid]
                   ) in
    let [id] = [ fold<'a. cont<cont<'a>> + (cont<cont<int>> + int) * 'a> (inl(thunk)) ] in
        [id]
    
letu zipWith = fun f -> 
  fix (fun zw -> fun l1 -> fun l2 ->
     let [v1] = l1 in
     let [v2] = l2 in
       cons (f (head [v1]) (head [v2])) (zw (tail [v1]) (tail [v2])))

letu fib =
  (fix ( (fun f ->
       let [v] = thunk f in
       cons [0] (cons [1] (zipWith (fun x -> fun y -> 
                                      let [xv] = x in
                                      let [yv] = y in [xv+yv])
                               [v] (tail [v])) ))))

letu cycle =
  (fix (fun cycle -> cons [1] cycle))

letu printlist =
  fun l ->
  fun j ->
  let [jv] = j in
  fix (fun p -> 
         fun i ->
         fun l -> 
           let [iv] = i in
           if iv = jv then [()] 
             else
                let [lid] = l in 
                let [xv] = head [lid] in
                let [u] = [print xv] in
                let [u] = [if iv + 1 < jv then print ", " else ()] in
                  p [iv + 1] (tail [lid])) 
    [0] l

letu aux =
  fix (fun aux ->
         fun n ->
           copy n as n1, n2 in
             cons [1] (cons n1 (cons [1] (aux (let [nv] = n2 in [nv + 2])))))

letu eContFrac =  (cons [2] (aux [2]))

letw signum = fun x -> if 0 < x then 1 else if x = 0 then 0 else 0-1
letw abs = fun x -> if 0 < x then x else 0-x
letw true : 2 = inl()
letw false : 2 = inr()
letw not : 2 -> 2 =
  fun x -> if x then false else true
letw and : 2 -> 2 -> 2 = 
  fun x -> fun y ->
    if x then y else false
letw or = fun (x : 2) -> fun (y : 2) ->
  if x then true else y

letu ratTrans =
   (fix ((fun ratTrans ->
          fun a -> fun b -> fun c -> fun d -> fun xs ->
             let [av] = a in
             let [bv] = b in
             let [cv] = c in
             let [dv] = d in
             let [qv] = [if dv = 0 then 1 else bv/dv] in
             let [c] = [
              and (or (signum cv = signum dv) (abs cv < abs dv)) 
                      (and ((cv+dv)*qv < av+bv+1) 
                           (av+bv < (cv+dv)*qv + (cv+dv)))] in
             if c then
                   cons [qv] (ratTrans [cv] [dv] [av-qv*cv] [bv-qv*dv] xs)
             else 
                   copy xs as xs1, xs2 in
                   let [xv] = head xs1 in
                    ratTrans [bv] [av+xv*bv] [dv] [cv+xv*dv] (tail xs2)
   )))

letu toDigits = 
   fix ((fun toDigits ->
          fun l ->
            let [lid] = l in
            cons (head [lid]) (toDigits (ratTrans [10] [0] [0] [1] (tail [lid])))))

letu e = (toDigits eContFrac)

letu ttt = printlist e [11]
letu ttu = printlist eContFrac [2100]
